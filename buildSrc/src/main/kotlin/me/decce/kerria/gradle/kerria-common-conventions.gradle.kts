package me.decce.kerria.gradle

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import dev.kikugie.stonecutter.build.StonecutterBuildExtension

plugins {
    `java-library`
    id("com.gradleup.shadow")
    id("me.modmuss50.mod-publish-plugin")
}

fun prop(name: String) = if (hasProperty(name)) findProperty(name) as String else throw IllegalArgumentException("$name not found")
val shade = configurations.create("shade")
configurations.implementation.get().extendsFrom(shade)

val platform = prop("deps.platform")
fun fullModVersion() = "${prop("mod_version")}+${prop("deps.minecraft")}-${platform}"

val stonecutter = project.extensions.getByType<StonecutterBuildExtension>()
val mcVersion = stonecutter.current.version
val javaVersion =
    if (stonecutter.eval(mcVersion, ">=26")) 25
    else if (stonecutter.eval(mcVersion, ">=1.20.5")) 21
    else if (stonecutter.eval(mcVersion, ">=1.18")) 17
    else 17 // TODO: maybe support Java 8 for 1.16?

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    targetCompatibility = JavaVersion.toVersion(javaVersion)
}

version = fullModVersion()
group = prop("maven_group")
base {
    archivesName = prop("mod_name")
}

fun fetchLatestChangelog() : String {
    val str = providers.fileContents(layout.settingsDirectory.file("CHANGELOG.md")).asText.get()
    val first = str.indexOf("## ")
    val i = str.indexOf('\n', first) + 2
    var r = str.indexOf("\n## ", i + 1)
    if (r == -1) r = str.length
    return str.substring(i, r - 1)
}

fun supportedVersionFabric() : String {
    var str = ""
    if (project.hasProperty("minecraft_supported_from")) {
        str += ">=${prop("minecraft_supported_from")}"
    }
    if (project.hasProperty("minecraft_supported_to")) {
        str += " <=${prop("minecraft_supported_to")}"
    }
    if (str == "") {
        str = "=${prop("deps.minecraft")}"
    }
    return str;
}

fun supportedVersionForge() : String {
    var str = "["
    if (hasProperty("minecraft_supported_from")) {
        str += "${prop("minecraft_supported_from")},"
    }
    if (hasProperty("minecraft_supported_to")) {
        str += prop("minecraft_supported_to")
    }
    if (str == "[") {
        str += prop("deps.minecraft")
    }
    str += "]"
    return str;
}

repositories {
    fun exclusiveMaven(mavenName: String, group: String, mavenUrl: String) {
        exclusiveContent {
            forRepository {
                maven {
                    name = mavenName
                    url = uri(mavenUrl)
                }
            }
            filter {
                includeGroupAndSubgroups(group)
            }
        }
    }
    exclusiveMaven("CaffeineMC", "net.caffeinemc", "https://maven.caffeinemc.net/releases")
    exclusiveMaven("Modrinth Maven", "maven.modrinth", "https://api.modrinth.com/maven")
}

dependencies {
    shade ("com.github.ben-manes.caffeine:caffeine:3.2.3") {
        isTransitive = false
    }
}

tasks {
    named<ShadowJar>("shadowJar") {
        archiveClassifier = "fat"
        configurations = listOf(shade)
        relocate("com.github.benmanes.caffeine", "me.decce.kerria.shadow.caffeine")
        from(layout.settingsDirectory.dir("thirdparty/licenses"))
    }

    withType<ProcessResources> {
        if (platform != "fabric") exclude("**/fabric.mod.json")
        if (platform != "neoforge") exclude("**/neoforge.mods.toml")
        if (platform != "forge") exclude("**/mods.toml", "**/pack.mcmeta")
        val propMap = mutableMapOf<String, Any>().apply {
            project.properties.forEach { k, v -> put(k.toString(), v.toString()) }
            put("mod_version_full", fullModVersion())
            put("minecraft_supported_fabric", supportedVersionFabric())
            put("minecraft_supported_forge", supportedVersionForge())
            put("java_version", javaVersion)
        }
        inputs.property("propMap", propMap)
        filesMatching(listOf("**/fabric.mod.json", "**/neoforge.mods.toml", "**/mods.toml", "**/pack.mcmeta")) {
            expand(propMap)
        }
    }

    named<ProcessResources>("processResources") {
        from (layout.settingsDirectory.file("LICENSE"))
    }
}

publishMods {
    type = STABLE
    version = fullModVersion()
    dryRun = providers.environmentVariable("CURSEFORGE_TOKEN").getOrNull() == null || providers.environmentVariable("MODRINTH_TOKEN").getOrNull() == null
    changelog = fetchLatestChangelog()
    displayName = "${prop("mod_name")} ${fullModVersion()}"
    modLoaders.add(prop("deps.platform"))
    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        clientRequired = true
        serverRequired = false
        projectId = "1503432"
        projectSlug = "kerria"
        if (hasProperty("minecraft_supported_from")) {
            minecraftVersionRange {
                start = prop("minecraft_supported_from")
                end = if (hasProperty("minecraft_supported_to")) prop("minecraft_supported_to") else "latest"
            }
        }
        else {
            minecraftVersions.add(prop("deps.minecraft"))
        }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "f0ruQTF7"
        if (hasProperty("minecraft_supported_from")) {
            minecraftVersionRange {
                start = prop("minecraft_supported_from")
                end = if (hasProperty("minecraft_supported_to")) prop("minecraft_supported_to") else "latest"
            }
        }
        else {
            minecraftVersions.add(prop("deps.minecraft"))
        }
    }
}